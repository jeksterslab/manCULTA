TITLE:
  2-Profile LTA with Covariate (Final);

DATA:
  FILE = __DATA__;

VARIABLE:
  NAMES =
    id x
    y1t0 y2t0 y3t0 y4t0 y1t1 y2t1 y3t1 y4t1
    y1t2 y2t2 y3t2 y4t2 y1t3 y2t3 y3t3 y4t3
    y1t4 y2t4 y3t4 y4t4 y1t5 y2t5 y3t5 y4t5
  ;
  USEVARIABLES =
    x
    y1t0 y2t0 y3t0 y4t0 y1t1 y2t1 y3t1 y4t1
    y1t2 y2t2 y3t2 y4t2 y1t3 y2t3 y3t3 y4t3
    y1t4 y2t4 y3t4 y4t4 y1t5 y2t5 y3t5 y4t5
  ;
  IDVARIABLE = id;
  CLASSES = c0(2) c1(2) c2(2) c3(2) c4(2) c5(2);
  MISSING = .;

DEFINE:
  STANDARDIZE
    y1t0 y2t0 y3t0 y4t0 y1t1 y2t1 y3t1 y4t1
    y1t2 y2t2 y3t2 y4t2 y1t3 y2t3 y3t3 y4t3
    y1t4 y2t4 y3t4 y4t4 y1t5 y2t5 y3t5 y4t5
  ;

ANALYSIS:
  TYPE = MIXTURE;
  STARTS = 1000 500;
  STSCALE = 2;
  STITERATIONS = 200;
  PROCESS = __CORES__;
  MODEL = NOCOV;

MODEL:
  %OVERALL%
    ! unique states -----------------------------------------------------

    !! variances
    !!! t = 0
    y1t0 (theta11);
    y2t0 (theta22);
    y3t0 (theta33);
    y4t0 (theta44);
    !!! t = 1
    y1t1 (theta11);
    y2t1 (theta22);
    y3t1 (theta33);
    y4t1 (theta44);
    !!! t = 2
    y1t2 (theta11);
    y2t2 (theta22);
    y3t2 (theta33);
    y4t2 (theta44);
    !!! t = 3
    y1t3 (theta11);
    y2t3 (theta22);
    y3t3 (theta33);
    y4t3 (theta44);
    !!! t = 4
    y1t4 (theta11);
    y2t4 (theta22);
    y3t4 (theta33);
    y4t4 (theta44);
    !!! t = 5
    y1t5 (theta11);
    y2t5 (theta22);
    y3t5 (theta33);
    y4t5 (theta44);

    ! lta ---------------------------------------------------------------

    !! initial profile membership
    [ c0#1 ] (nu0);
    c0#1 ON x (kappa0);

    !! profile transitions
    [ c1#1 ] (alpha0);
    [ c2#1 ] (alpha0);
    [ c3#1 ] (alpha0);
    [ c4#1 ] (alpha0);
    [ c5#1 ] (alpha0);
    c1#1 ON c0#1 (beta00);
    c2#1 ON c1#1 (beta00);
    c3#1 ON c2#1 (beta00);
    c4#1 ON c3#1 (beta00);
    c5#1 ON c4#1 (beta00);

MODEL c0:
  %c0#1%
    ! profile specific means
    [ y1t0 ] (mu10);
    [ y2t0 ] (mu20);
    [ y3t0 ] (mu30);
    [ y4t0 ] (mu40);

    ! covariate
    c1 ON x (gamma00);

  %c0#2%
    ! profile specific means
    [ y1t0 ] (mu11);
    [ y2t0 ] (mu21);
    [ y3t0 ] (mu31);
    [ y4t0 ] (mu41);

    ! covariate
    c1 ON x (gamma10);

MODEL c1:
  %c1#1%
    ! profile specific means
    [ y1t1 ] (mu10);
    [ y2t1 ] (mu20);
    [ y3t1 ] (mu30);
    [ y4t1 ] (mu40);

    ! covariate
    c2 ON x (gamma00);

  %c1#2%
    ! profile specific means
    [ y1t1 ] (mu11);
    [ y2t1 ] (mu21);
    [ y3t1 ] (mu31);
    [ y4t1 ] (mu41);

    ! covariate
    c2 ON x (gamma10);

MODEL c2:
  %c2#1%
    ! profile specific means
    [ y1t2 ] (mu10);
    [ y2t2 ] (mu20);
    [ y3t2 ] (mu30);
    [ y4t2 ] (mu40);

    ! covariate
    c3 ON x (gamma00);

  %c2#2%
    ! profile specific means
    [ y1t2 ] (mu11);
    [ y2t2 ] (mu21);
    [ y3t2 ] (mu31);
    [ y4t2 ] (mu41);

    ! covariate
    c3 ON x (gamma10);

MODEL c3:
  %c3#1%
    ! profile specific means
    [ y1t3 ] (mu10);
    [ y2t3 ] (mu20);
    [ y3t3 ] (mu30);
    [ y4t3 ] (mu40);

    ! covariate
    c4 ON x (gamma00);

  %c3#2%
    ! profile specific means
    [ y1t3 ] (mu11);
    [ y2t3 ] (mu21);
    [ y3t3 ] (mu31);
    [ y4t3 ] (mu41);

    ! covariate
    c4 ON x (gamma10);

MODEL c4:
  %c4#1%
    ! profile specific means
    [ y1t4 ] (mu10);
    [ y2t4 ] (mu20);
    [ y3t4 ] (mu30);
    [ y4t4 ] (mu40);

    ! covariate
    c5 ON x (gamma00);

  %c4#2%
    ! profile specific means
    [ y1t4 ] (mu11);
    [ y2t4 ] (mu21);
    [ y3t4 ] (mu31);
    [ y4t4 ] (mu41);

    ! covariate
    c5 ON x (gamma10);

MODEL c5:
  %c5#1%
    ! profile specific means
    [ y1t5 ] (mu10);
    [ y2t5 ] (mu20);
    [ y3t5 ] (mu30);
    [ y4t5 ] (mu40);

  %c5#2%
    ! profile specific means
    [ y1t5 ] (mu11);
    [ y2t5 ] (mu21);
    [ y3t5 ] (mu31);
    [ y4t5 ] (mu41);

MODEL CONSTRAINT:
  ! means for the first profile are higher than the second
  mu10 > mu11;
  mu20 > mu21;
  mu30 > mu31;
  mu40 > mu41;

  ! make sure variances are greater than zero
  theta11 > 0;
  theta22 > 0;
  theta33 > 0;
  theta44 > 0;

OUTPUT:
  TECH1 TECH3 TECH4 TECH7 TECH8 TECH12 TECH15 ENTROPY;

SAVEDATA:
  ESTIMATES = __ESTIMATES__;
  RESULTS = __RESULTS__;
  TECH3 = __TECH3__;
  TECH4 = __TECH4__;
  FILE = __CPROB__;
  SAVE = CPROBABILITIES;

TITLE: CUTS;

DATA:
    FILE = __DATA__;

VARIABLE:
    NAMES = id
        aud          ! audit
        p0 r0 f0 d0  ! p? = peak r? = rise f? = fall d? = duration
        p1 r1 f1 d1  ! ? = time 0, 1, 2, 3, 4, 5
        p2 r2 f2 d2
        p3 r3 f3 d3
        p4 r4 f4 d4
        p5 r5 f5 d5;
    MISSING = .;
    USEVAR = p0 r0 f0 d0
        p1 r1 f1 d1
        p2 r2 f2 d2
        p3 r3 f3 d3
        p4 r4 f4 d4
        p5 r5 f5 d5;

DEFINE:
    STANDARDIZE p0 r0 f0 d0
        p1 r1 f1 d1
        p2 r2 f2 d2
        p3 r3 f3 d3
        p4 r4 f4 d4
        p5 r5 f5 d5;

ANALYSIS:
    STARTS = 200;
    MODEL = NOCOV;

MODEL:

    !! Common stable trait
    t BY p0@1;
    t BY r0 (tr);
    t BY f0 (tf);
    t BY d0 (td);

    t BY p1@1;
    t BY r1 (tr);
    t BY f1 (tf);
    t BY d1 (td);

    t BY p2@1;
    t BY r2 (tr);
    t BY f2 (tf);
    t BY d2 (td);

    t BY p3@1;
    t BY r3 (tr);
    t BY f3 (tf);
    t BY d3 (td);

    t BY p4@1;
    t BY r4 (tr);
    t BY f4 (tf);
    t BY d4 (td);

    t BY p5@1;
    t BY r5 (tr);
    t BY f5 (tf);
    t BY d5 (td);

    [t@0];
    t (pt);

    !! Common states

    !! Invariance constraints on factor loadings
    s0 BY p0@1;
    s0 BY r0 (sr);
    s0 BY f0 (sf);
    s0 BY d0 (sd);

    s1 BY p1@1;
    s1 BY r1 (sr);
    s1 BY f1 (sf);
    s1 BY d1 (sd);

    s2 BY p2@1;
    s2 BY r2 (sr);
    s2 BY f2 (sf);
    s2 BY d2 (sd);

    s3 BY p3@1;
    s3 BY r3 (sr);
    s3 BY f3 (sf);
    s3 BY d3 (sd);

    s4 BY p4@1;
    s4 BY r4 (sr);
    s4 BY f4 (sf);
    s4 BY d4 (sd);

    s5 BY p5@1;
    s5 BY r5 (sr);
    s5 BY f5 (sf);
    s5 BY d5 (sd);

    [s0-s5@0];
    s0 (pst0);
    s1-s5 (ps);

    !! Invariance constraints on intercepts
    [p0] (mp);
    [r0] (mr);
    [f0] (mf);
    [d0] (md);

    [p1] (mp);
    [r1] (mr);
    [f1] (mf);
    [d1] (md);

    [p2] (mp);
    [r2] (mr);
    [f2] (mf);
    [d2] (md);

    [p3] (mp);
    [r3] (mr);
    [f3] (mf);
    [d3] (md);

    [p4] (mp);
    [r4] (mr);
    [f4] (mf);
    [d4] (md);

    [p5] (mp);
    [r5] (mr);
    [f5] (mf);
    [d5] (md);

    !! Invariance constraints on residual variances
    p0 (thp);
    r0 (thr);
    f0 (thf);
    d0 (thd);

    p1 (thp);
    r1 (thr);
    f1 (thf);
    d1 (thd);

    p2 (thp);
    r2 (thr);
    f2 (thf);
    d2 (thd);

    p3 (thp);
    r3 (thr);
    f3 (thf);
    d3 (thd);

    p4 (thp);
    r4 (thr);
    f4 (thf);
    d4 (thd);

    p5 (thp);
    r5 (thr);
    f5 (thf);
    d5 (thd);

    !! Unique traits
    up BY p0@1 p1@1 p2@1 p3@1 p4@1 p5@1;
    ur BY r0@1 r1@1 r2@1 r3@1 r4@1 r5@1;
    uf BY f0@1 f1@1 f2@1 f3@1 f4@1 f5@1;
    ud BY d0@1 d1@1 d2@1 d3@1 d4@1 d5@1;

    [up@0];
    [ur@0];
    [uf@0];
    [ud@0];
    up (pup);
    ur (pur);
    uf (puf);
    ud (pud);

    !! AR
    s1 ON s0 (b);
    s2 ON s1 (b);
    s3 ON s2 (b);
    s4 ON s3 (b);
    s5 ON s4 (b);

MODEL CONSTRAINT:
    ! Make sure variances are greater than zero
    pt > 0;
    pst0 > 0;
    ps > 0;
    pup > 0;
    pur > 0;
    puf > 0;
    pud > 0;
    thp > 0;
    thr > 0;
    thf > 0;
    thd > 0;
